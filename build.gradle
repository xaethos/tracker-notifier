// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.versions = [
            sdk       : [
                    compile: 23,
                    min    : 15,
                    target : 23
            ],
            buildTools: "23.0.1",

            kotlin    : "1.0.0-+",

            support   : "23.1.0",
            rx        : "1.0.1",
            dagger    : "2.0.2",
            retrofit  : "2.0.0-beta1",
            junit     : "4.12"
    ]

    repositories {
        jcenter()
        mavenCentral() // Required because kotlin is on maven central only
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath 'com.android.tools.build:gradle:1.4.+'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        jcenter()
        mavenCentral() // Required because kotlin is on maven central only
    }
}

if (project.hasProperty('preDexLibraries')) {
    def preDexLibraries = project.property('preDexLibraries').toBoolean()

    subprojects {
        project.plugins.whenPluginAdded { plugin ->
            if ("com.android.build.gradle.AppPlugin".equals(plugin.class.name)) {
                project.android.dexOptions.preDexLibraries = preDexLibraries
            } else if ("com.android.build.gradle.LibraryPlugin".equals(plugin.class.name)) {
                project.android.dexOptions.preDexLibraries = preDexLibraries
            }
        }
    }
}
